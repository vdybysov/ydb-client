plugins {
    id 'java'
}

group = 'com.github.vdybysov.ydb'
version = '0.0.1'

subprojects {

    repositories {
        mavenCentral()
    }

    apply plugin: 'java'
    apply plugin: 'maven-publish'

    dependencies {
        compileOnly "org.projectlombok:lombok:${lombokVersion}"
        annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
        implementation 'commons-beanutils:commons-beanutils:1.9.4'
        implementation 'com.google.guava:guava:33.0.0-jre'
        implementation 'org.jetbrains:annotations:24.1.0'
        implementation 'tech.ydb:ydb-proto-api:1.6.1'

        testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
        testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
        testImplementation platform('org.junit:junit-bom:5.9.1')
        testImplementation 'org.junit.jupiter:junit-jupiter'
    }

    tasks.withType(JavaCompile).configureEach {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
        options.encoding = 'UTF-8'
    }

    test {
        useJUnitPlatform()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                artifactId = project.name
                groupId = project.parent.group
                version = project.parent.version
            }
        }
        repositories {
            mavenLocal()
        }
    }
}